/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - Ballerina Crypto'

def packageName = "crypto"
def packageOrg = "ballerina"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
}

configurations {
    externalJars
    externalFipsJars
}

dependencies {
    externalJars(group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: "${bouncyCastleVersion}") {
        transitive = false
    }
    externalJars(group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: "${bouncyCastleVersion}") {
        transitive = false
    }
    externalJars(group: 'org.bouncycastle', name: 'bcutil-jdk18on', version: "${bouncyCastleVersion}") {
        transitive = false
    }
    externalJars(group: 'org.bouncycastle', name: 'bcpg-jdk18on', version: "${bouncyCastleVersion}") {
        transitive = false
    }
    externalFipsJars(group: 'org.bouncycastle', name: 'bcpkix-fips', version: "${bouncyCastlePkixFipsVersion}") {
        transitive = false
    }
    externalFipsJars(group: 'org.bouncycastle', name: 'bc-fips', version: "${bouncyCastleFipsVersion}") {
        transitive = false
    }
    externalFipsJars(group: 'org.bouncycastle', name: 'bcpg-fips', version: "${bouncyCastlePgFipsVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def stdlibDependentBouncycastleVersion = project.bouncyCastleVersion

        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        newBallerinaToml = newBallerinaToml.replace("@bouncycastle.version@", stdlibDependentBouncycastleVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

task copyFipsJars(type: Copy) {
    if (project.configurations.find { it.name == "externalFipsJars" }) {
        into "$project.projectDir/fips/java21"
        from project.configurations.externalFipsJars
    }
    into "$project.projectDir/fips/java21"
    from "$project.projectDir/../fips-native/build/libs/crypto-fips-native-${project.version}.jar"
    rename "crypto-fips-native-${project.version}.jar", "crypto-native-${project.version}.jar"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

copyFipsJars.dependsOn ":${packageName}-fips-native:build"

updateTomlFiles.dependsOn copyStdlibs
updateTomlFiles.dependsOn copyFipsJars

test.dependsOn ":${packageName}-native:build"
test.dependsOn ":${packageName}-fips-native:build"

build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
build.dependsOn ":${packageName}-fips-native:build"

publishToMavenLocal.dependsOn build
publish.dependsOn build
